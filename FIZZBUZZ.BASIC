10 REM *** FIZZ BUZZ IN APPLESOFT BASIC
20 REM *** BY MIKE SHECKET, GITHUB.COM/MSHECKET
30 REM *** RUNNABLE AT HTTPS://WWW.CALORMEN.COM/JSBASIC
40 PRINT "WHAT NUMBER DO YOU WANT TO START WITH?"
50 INPUT A1
60 PRINT "WHAT NUMBER DO YOU WANT TO END WITH?"
70 INPUT A2
80 PRINT "WHAT MULTIPLES SHOULD FIZZ?"
90 INPUT FIZZ
100 PRINT "WHAT MULTIPLES SHOULD BUZZ?"
110 INPUT BUZZ
120 PRINT "READY...";
130 FOR I = 1 TO 3000: NEXT I : REM *** CLASSIC WAY TO IMPLEMENT A DRAMATIC PAUSE
140 PRINT "SET...";
150 FOR I = 1 TO 3000: NEXT I
160 PRINT "GO!"
170 PRINT
180 FOR I = A1 TO A2
190 REM *** THE INT FUNCTION IS THE INTEGER FLOOR--LIKE MATH.FLOOR()--SO
200 REM *** IF I IS A MULTIPLE OF FIZZ, THERE WILL BE NO DIFFERENCE BETWEEN
210 REM *** THE ROUNDED NUMBER AND THE NUMBER ITSELF
220 IF (I / FIZZ) - INT (I / FIZZ) = 0 THEN GOSUB 400
230 IF (I / BUZZ) - INT (I / BUZZ) = 0 THEN GOSUB 500
240 IF NOT ((I / FIZZ) - INT (I / FIZZ) = 0) AND NOT ((I / BUZZ) - INT(I / BUZZ) = 0) THEN PRINT I;
250 PRINT
260 NEXT I
270 PRINT "TA DA! COPYRIGHT MIKE SHECKET 1986ISH"
300 END
400 PRINT "FIZZ";
410 RETURN
500 PRINT "BUZZ";
510 RETURN
