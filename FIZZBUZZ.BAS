10 REM *** FIZZ BUZZ IN APPLESOFT BASIC
20 REM *** BY MIKE SHECKET, GITHUB.COM/MSHECKET
30 REM *** RUNNABLE AT HTTPS://WWW.CALORMEN.COM/JSBASIC
40 PRINT "WHAT NUMBER DO YOU WANT TO START WITH?"
50 INPUT A1 : REM *** GET USER INPUT FOR FIRST NUMBER
60 PRINT "WHAT NUMBER DO YOU WANT TO END WITH?"
70 INPUT A2 : REM *** GET USER INPUT FOR LAST NUMBER
80 PRINT "WHAT MULTIPLES SHOULD FIZZ?"
90 INPUT FIZZ : REM *** GET USER INPUT FOR FIZZ MULTIPLE
100 PRINT "WHAT MULTIPLES SHOULD BUZZ?"
110 INPUT BUZZ : REM *** GET USER INPUT FOR BUZZ MULTIPLE
120 PRINT "READY...";
130 FOR I = 1 TO 3000: NEXT I : REM *** CLASSIC WAY TO IMPLEMENT A DRAMATIC PAUSE
140 PRINT "SET...";
150 FOR I = 1 TO 3000: NEXT I : REM *** ONE MORE TO BUILD THE TENSION...
160 PRINT "GO!"
170 PRINT : REM *** LEAVE AN EXTRA SPACE
180 FOR I = A1 TO A2 : REM *** ITERATE FROM THE FIRST INTEGER TO THE LAST
190 REM *** THE INT FUNCTION IS THE INTEGER FLOOR--LIKE MATH.FLOOR()--SO
200 REM *** IF I IS A MULTIPLE OF FIZZ, THERE WILL BE NO DIFFERENCE BETWEEN
210 REM *** THE ROUNDED NUMBER AND THE NUMBER ITSELF
220 IF (I / FIZZ) - INT (I / FIZZ) = 0 THEN GOSUB 400 : REM *** IF IT IS A MULTIPLE, GO TO THE SUBROUTINE AT 300
230 IF (I / BUZZ) - INT (I / BUZZ) = 0 THEN GOSUB 500 : REM *** SAME WITH BUZZ
240 REM *** IF I IS NOT A MULTIPLE OF EITHER FIZZ OR BUZZ, THEN JUST PRINT THE NUMBER
250 REM *** BUT NOT A CARRIAGE RETURN YET
260 IF NOT ((I / FIZZ) - INT (I / FIZZ) = 0) AND NOT ((I / BUZZ) - INT(I / BUZZ) = 0) THEN PRINT I;
270 PRINT : REM *** NOW PRINT THE CARRIAGE RETURN
280 NEXT I : REM *** AND GO ON TO THE NEXT NUMBER I
290 PRINT "TA DA! COPYRIGHT MIKE SHECKET 1986ISH"
300 END
400 PRINT "FIZZ"; : REM *** FIZZING SUBROUTINE
410 RETURN
500 PRINT "BUZZ"; : REM *** BUZZING SUBROUTINE
510 RETURN
